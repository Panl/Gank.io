apply plugin: 'com.android.application'

def releaseTime(){
    return new Date().format("yyyy-MM-dd",TimeZone.getTimeZone("UTC"))
}

Properties properties = new Properties()
properties.load(project.rootProject.file('config.properties').newDataInputStream())

def keyStore = file(properties.getProperty("STORE_PATH"))


android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.smartalk.gank"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 2
        versionName "1.0.1"
    }

    signingConfigs {
        release {
            storeFile keyStore
            storePassword properties.getProperty("STORE_PASSWORD")
            keyAlias properties.getProperty("KEY_ALIAS")
            keyPassword properties.getProperty("KEY_PASSWORD")
        }
    }

    productFlavors{
        xiaomi{
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"xiaomi"]
        }
        wandoujia{
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"wandoujia"]
        }
        fir{
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"fir"]
        }
    }



    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "Gank_io_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }


}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.daimajia.numberprogressbar:library:1.2@aar'
    compile 'com.github.mthli:Slice:v1.0'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.umeng.analytics:analytics:latest.integration'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1' // or 1.4-beta1
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1' // or 1.4-beta1
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
